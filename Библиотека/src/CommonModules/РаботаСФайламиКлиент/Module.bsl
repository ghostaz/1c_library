
#Область РасширениеРаботыСФайлами

Процедура ПроверитьРасширениеРаботыСФайлами(ТекстВопроса, ОписаниеОповещения = Неопределено)Экспорт
	
	НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ПроверитьРасширениеРаботыСФайламиЗавершение", ЭтотОбъект, Новый Структура("ТекстВопроса, ОписаниеОповещения", ТекстВопроса, ОписаниеОповещения)));
	
КонецПроцедуры

Процедура ПроверитьРасширениеРаботыСФайламиЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	ТекстВопроса = ДополнительныеПараметры.ТекстВопроса;
		
	Если Не Подключено Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПослеЗакрытияВопросаПроверитьРасширениеРаботыСФайлами", РаботаСФайламиКлиент, Новый Структура("ОписаниеОповещения", ДополнительныеПараметры.ОписаниеОповещения)), ТекстВопроса, РежимДиалогаВопрос.ДаНет, , , "Предупреждение");
	ИначеЕсли ДополнительныеПараметры.ОписаниеОповещения <> Неопределено Тогда // Если вдруг просто надо подключить расширение без перехода куда либо
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения, Подключено);
	КонецЕсли;

КонецПроцедуры

Процедура ПослеЗакрытияВопросаПроверитьРасширениеРаботыСФайлами(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		НачатьУстановкуРасширенияРаботыСФайлами();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаФайла

Процедура ЗагрузитьФайл(ПроцедураДляВозвратаНаФорму, УникальныйИдентификаторФормы, ВременныйФайл = Ложь) Экспорт
	
	ТекстВопроса = НСтр("ru = 'Для загрузки файла необходимо установить расширение работы с файлами.'");
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификаторФормы);
	ДополнительныеПараметры.Вставить("ПроцедураДляВозвратаНаФорму", ПроцедураДляВозвратаНаФорму);
	ДополнительныеПараметры.Вставить("ВременныйФайл", ВременныйФайл);
	ПроверитьРасширениеРаботыСФайлами(ТекстВопроса, Новый ОписаниеОповещения("ПодключитьРасширениеЗавершение", ЭтотОбъект, ДополнительныеПараметры));

КонецПроцедуры

Процедура ПодключитьРасширениеЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗагрузитьЗавершение", ЭтотОбъект, ДополнительныеПараметры), , , , ДополнительныеПараметры.УникальныйИдентификаторФормы);
	КонецЕсли;

КонецПроцедуры

Процедура ЗагрузитьЗавершение(Результат, АдресВХранилище, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
		
		КороткоеИмяФайла = РаботаСФайламиКлиентСервер.КороткоеИмяФайлаИзПолногоПути(ВыбранноеИмяФайла);
		
		Если ДополнительныеПараметры.ВременныйФайл Тогда
			Файл = РаботаСФайламиВызовСервера.ЗагрузитьВременныйФайл(АдресВХранилище, КороткоеИмяФайла);
		Иначе
			Файл = РаботаСФайламиВызовСервера.ЗагрузитьФайл(АдресВХранилище, КороткоеИмяФайла);
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ПроцедураДляВозвратаНаФорму, Файл); // Вернемся на ту форму из которой была вызвана процедура ЗагрузитьФайл
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти