
// Скачивает архив книг с сайта OZON, разбирает и записывает в таблицу КаталогКниг
//
// Параметры:
//  АдресФайлаКаталогаКниг - Строка - Путь к файлу каталога книг на диске. Если не указан,
//  	то производится его загрузка из интернета
//
Процедура ЗаполнитьКаталогКниг(АдресФайлаКаталогаКниг = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(АдресФайлаКаталогаКниг) Тогда
		ИмяВременногоФайлаАрхива = АдресФайлаКаталогаКниг;
	Иначе
		АдресФайлаКаталогаКниг = Константы.АдресФайлаКаталогаКниг.Получить();
		ИмяВременногоФайлаАрхива = ОбщегоНазначенияСервер.ЗагрузитьФайлПоСсылке(АдресФайлаКаталогаКниг);
	КонецЕсли;
	
	ИмяВременногоФайлаКаталогаКниг = ПолучитьИмяВременногоФайла();
	АрхивКаталогаКниг = Новый ЧтениеZipФайла(ИмяВременногоФайлаАрхива);
	Попытка
		АрхивКаталогаКниг.ИзвлечьВсе(ИмяВременногоФайлаКаталогаКниг, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
	Исключение
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Не удалось распаковать каталог книг'");
		СообщениеПользователю.Сообщить();
		ОписаниеОшибки = ОписаниеОшибки();
		ОбщегоНазначенияСервер.ЗафиксироватьОшибку(СообщениеПользователю.Текст, ОписаниеОшибки, Метаданные.РегистрыСведений.КаталогКниг, Неопределено);
		Возврат;
	КонецПопытки;
	АрхивКаталогаКниг.Закрыть();
	
	Файл = Новый Файл(ИмяВременногоФайлаКаталогаКниг);
	
	Если Не Файл.Существует() Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Не удалось распаковать каталог книг'");
		СообщениеПользователю.Сообщить();
		ОбщегоНазначенияСервер.ЗафиксироватьОшибку(СообщениеПользователю.Текст, СообщениеПользователю.Текст, Метаданные.РегистрыСведений.КаталогКниг, Неопределено);
		Возврат;
	КонецЕсли;
	
	Если Файл.ЭтоКаталог() Тогда 
		МасcивФайловКаталога = НайтиФайлы(ИмяВременногоФайлаКаталогаКниг, "*");
		Если МасcивФайловКаталога.Количество() > 0 Тогда
			ИмяВременногоФайлаКаталогаКниг = МасcивФайловКаталога[0].ПолноеИмя;
		КонецЕсли;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.КаталогКниг.СоздатьНаборЗаписей();
	
	КаталогКнигXML = Новый ЧтениеXML;
	КаталогКнигXML.ОткрытьФайл(ИмяВременногоФайлаКаталогаКниг);
	
	ИдентификаторКниги = Неопределено;
	Наименование = Неопределено;
	Описание = Неопределено;
	Штрихкод = Неопределено;
	Автор = Неопределено;
	Издательство = Неопределено;
	ISBN = Неопределено;
	ГодИздания = Неопределено;
	СсылкаНаСайт = Неопределено;
	СсылкаНаКартинку = Неопределено;
	
	Пока КаталогКнигXML.Прочитать() Цикл
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "offer" Тогда
			Пока КаталогКнигXML.ПрочитатьАтрибут() Цикл
				Если КаталогКнигXML.ТипУзла = ТипУзлаXML.Атрибут И КаталогКнигXML.Имя = "id" Тогда
					ИдентификаторКниги = КаталогКнигXML.Значение;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "url" Тогда 
			КаталогКнигXML.Прочитать();
			СсылкаНаСайт = КаталогКнигXML.Значение;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "name" Тогда 
			КаталогКнигXML.Прочитать();
			Наименование = КаталогКнигXML.Значение;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "description" Тогда 
			КаталогКнигXML.Прочитать();
			Описание = КаталогКнигXML.Значение;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "barcode" Тогда 
			КаталогКнигXML.Прочитать();
			Штрихкод = КаталогКнигXML.Значение;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "picture" Тогда 
			КаталогКнигXML.Прочитать();
			СсылкаНаКартинку = КаталогКнигXML.Значение;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "author" Тогда 
			КаталогКнигXML.Прочитать();
			Автор = КаталогКнигXML.Значение;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "publisher" Тогда 
			КаталогКнигXML.Прочитать();
			Издательство = КаталогКнигXML.Значение;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "ISBN" Тогда 
			КаталогКнигXML.Прочитать();
			ISBN = КаталогКнигXML.Значение;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "year" Тогда 
			КаталогКнигXML.Прочитать();
			ГодИздания = КаталогКнигXML.Значение;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И КаталогКнигXML.Имя = "param" Тогда 
			Пока КаталогКнигXML.ПрочитатьАтрибут() Цикл
				Если КаталогКнигXML.ТипУзла = ТипУзлаXML.Атрибут И КаталогКнигXML.Имя = "name" И КаталогКнигXML.Значение = "Автор" Тогда
					КаталогКнигXML.Прочитать();
					Автор = КаталогКнигXML.Значение;
				КонецЕсли;
				Если КаталогКнигXML.ТипУзла = ТипУзлаXML.Атрибут И КаталогКнигXML.Имя = "name" И КаталогКнигXML.Значение = "Публикации" Тогда
					КаталогКнигXML.Прочитать();
					Описание = КаталогКнигXML.Значение;
				КонецЕсли;
				Если КаталогКнигXML.ТипУзла = ТипУзлаXML.Атрибут И КаталогКнигXML.Имя = "name" И КаталогКнигXML.Значение = "Издательство" Тогда
					КаталогКнигXML.Прочитать();
					Издательство = КаталогКнигXML.Значение;
				КонецЕсли;
				Если КаталогКнигXML.ТипУзла = ТипУзлаXML.Атрибут И КаталогКнигXML.Имя = "name" И КаталогКнигXML.Значение = "ISBN" Тогда
					КаталогКнигXML.Прочитать();
					ISBN = КаталогКнигXML.Значение;
				КонецЕсли;
				Если КаталогКнигXML.ТипУзла = ТипУзлаXML.Атрибут И КаталогКнигXML.Имя = "name" И КаталогКнигXML.Значение = "Год выпуска" Тогда
					КаталогКнигXML.Прочитать();
					ГодИздания = КаталогКнигXML.Значение;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если КаталогКнигXML.ТипУзла = ТипУзлаXML.КонецЭлемента И КаталогКнигXML.Имя = "offer" Тогда
			
			НоваяКнигаКаталога = НаборЗаписей.Добавить();
			НоваяКнигаКаталога.ИдентификаторКниги = ИдентификаторКниги;
			НоваяКнигаКаталога.Наименование = Наименование;
			НоваяКнигаКаталога.Описание = Описание;
			НоваяКнигаКаталога.Штрихкод = Штрихкод;
			НоваяКнигаКаталога.Автор = Автор;
			НоваяКнигаКаталога.Издательство = Издательство;
			НоваяКнигаКаталога.ISBN = ISBN;
			НоваяКнигаКаталога.ISBNЧисло = СтрСоединить(СтрРазделить(Лев(ISBN, 13),"-",Ложь),"");
			НоваяКнигаКаталога.ГодИздания = ГодИздания;
			НоваяКнигаКаталога.СсылкаНаСайт = СтрЗаменить(СсылкаНаСайт, "prt_xml_facet", "ghostaz");
			НоваяКнигаКаталога.СсылкаНаКартинку = СсылкаНаКартинку;
			
			ИдентификаторКниги = Неопределено;
			Наименование = Неопределено;
			Описание = Неопределено;
			Штрихкод = Неопределено;
			Автор = Неопределено;
			Издательство = Неопределено;
			ISBN = Неопределено;
			ГодИздания = Неопределено;
			СсылкаНаСайт = Неопределено;
			СсылкаНаКартинку = Неопределено;
			
		КонецЕсли;
		
	КонецЦикла;
	НаборЗаписей.Записать(Истина);
	
	
КонецПроцедуры // ЗаполнитьКаталогКниг()

// Очищает регистр Каталог книг
//
// Параметры:
//  нет
//
Процедура ОчиститьКаталогКниг() Экспорт
	
	НаборЗаписей = РегистрыСведений.КаталогКниг.СоздатьНаборЗаписей();
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры // ОчиститьКаталогКниг()


